// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.30;

import { _1_USDC } from "../utils/Constants.sol";
import { DeploymentBaseTest } from "../utils/DeploymentBaseTest.sol";

import { IERC20 } from "forge-std/interfaces/IERC20.sol";

import { kBase } from "kam/src/base/kBase.sol";
import {
    KASSETROUTER_BATCH_ID_PROPOSED,
    KASSETROUTER_COOLDOOWN_IS_UP,
    KASSETROUTER_INSUFFICIENT_VIRTUAL_BALANCE,
    KASSETROUTER_INVALID_COOLDOWN,
    KASSETROUTER_IS_PAUSED,
    KASSETROUTER_ONLY_KMINTER,
    KASSETROUTER_ONLY_KSTAKING_VAULT,
    KASSETROUTER_PROPOSAL_NOT_FOUND,
    KASSETROUTER_WRONG_ROLE,
    KASSETROUTER_ZERO_ADDRESS,
    KASSETROUTER_ZERO_AMOUNT,
    KBASE_INVALID_REGISTRY,
    KBASE_WRONG_ROLE
} from "kam/src/errors/Errors.sol";
import { IkAssetRouter } from "kam/src/interfaces/IkAssetRouter.sol";
import { kAssetRouter } from "kam/src/kAssetRouter.sol";
import { ERC1967Factory } from "solady/utils/ERC1967Factory.sol";

contract kAssetRouterTest is DeploymentBaseTest {
    bytes32 internal constant TEST_BATCH_ID = bytes32(uint256(1));
    uint256 internal constant TEST_AMOUNT = 1000 * _1_USDC;
    uint256 internal constant TEST_PROFIT = 100 * _1_USDC;
    uint256 internal constant TEST_LOSS = 50 * _1_USDC;
    uint256 internal constant TEST_TOTAL_ASSETS = 10_000 * _1_USDC;
    // casting to 'int256' is safe because 500 * _1_USDC fits in int256
    // forge-lint: disable-next-line(unsafe-typecast)
    int256 internal constant TEST_NETTED = int256(500 * _1_USDC);

    address internal mockBatchReceiver = makeAddr("mockBatchReceiver");

    bytes32 internal testProposalId;

    function setUp() public override {
        super.setUp();

        // Set cooldown to 0 for testing
        vm.prank(users.admin);
        assetRouter.setSettlementCooldown(1); // Set to 1 second (minimum non-zero)
    }

    /* //////////////////////////////////////////////////////////////
                        INITIALIZATION TESTS
    //////////////////////////////////////////////////////////////*/

    function test_InitialState() public view {
        assertEq(assetRouter.contractName(), "kAssetRouter", "Contract name incorrect");
        assertEq(assetRouter.contractVersion(), "1.0.0", "Contract version incorrect");
        assertFalse(assetRouter.isPaused(), "Should be unpaused initially");
        assertEq(address(assetRouter.registry()), address(registry), "Registry not set correctly");
        assertEq(assetRouter.getSettlementCooldown(), 1, "Settlement cooldown not set correctly");
    }

    function test_Initialize_Success() public {
        // Deploy fresh implementation for testing
        kAssetRouter newAssetRouterImpl = new kAssetRouter();

        bytes memory initData = abi.encodeWithSelector(kAssetRouter.initialize.selector, address(registry));

        ERC1967Factory factory = new ERC1967Factory();
        address newProxy = factory.deployAndCall(address(newAssetRouterImpl), users.admin, initData);

        kAssetRouter newRouter = kAssetRouter(payable(newProxy));
        assertFalse(newRouter.isPaused(), "Should be unpaused");

        // Check default cooldown is set
        assertEq(newRouter.getSettlementCooldown(), 1 hours, "Default cooldown should be 1 hour");
    }

    function test_Initialize_RevertZeroRegistry() public {
        kAssetRouter newAssetRouterImpl = new kAssetRouter();

        bytes memory initData = abi.encodeWithSelector(kAssetRouter.initialize.selector, address(0));

        ERC1967Factory factory = new ERC1967Factory();
        vm.expectRevert(bytes(KBASE_INVALID_REGISTRY));
        factory.deployAndCall(address(newAssetRouterImpl), users.admin, initData);
    }

    function test_Initialize_RevertDoubleInit() public {
        vm.expectRevert();
        assetRouter.initialize(address(registry));
    }

    /* //////////////////////////////////////////////////////////////
                        KMINTER INTEGRATION TESTS
    //////////////////////////////////////////////////////////////*/

    function test_KAssetPush_Success() public {
        uint256 amount = TEST_AMOUNT;
        bytes32 batchId = TEST_BATCH_ID;

        // Fund minter with USDC
        mockUSDC.mint(address(minter), amount);

        // Approve asset router to spend
        vm.prank(address(minter));
        // forge-lint: disable-next-line(erc20-unchecked-transfer)
        IERC20(tokens.usdc).transfer(address(assetRouter), amount);

        // Test asset push
        vm.prank(address(minter));
        vm.expectEmit(true, false, false, true);
        emit IkAssetRouter.AssetsPushed(address(minter), amount);

        assetRouter.kAssetPush(tokens.usdc, amount, batchId);

        // Verify batch balance storage
        (uint256 deposited, uint256 requested) = assetRouter.getBatchIdBalances(address(minter), batchId);
        assertEq(deposited, amount, "Deposited amount incorrect");
        assertEq(requested, 0, "Requested should be zero");
    }

    function test_KAssetPush_RevertZeroAmount() public {
        vm.prank(address(minter));
        vm.expectRevert(bytes(KASSETROUTER_ZERO_AMOUNT));
        assetRouter.kAssetPush(tokens.usdc, 0, TEST_BATCH_ID);
    }

    function test_KAssetPush_RevertWhenPaused() public {
        // Pause contract
        vm.prank(users.emergencyAdmin);
        assetRouter.setPaused(true);

        vm.prank(address(minter));
        vm.expectRevert(bytes(KASSETROUTER_IS_PAUSED));
        assetRouter.kAssetPush(tokens.usdc, TEST_AMOUNT, TEST_BATCH_ID);
    }

    function test_KAssetPush_OnlyKMinter() public {
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_ONLY_KMINTER));
        assetRouter.kAssetPush(tokens.usdc, TEST_AMOUNT, TEST_BATCH_ID);
    }

    function test_KAssetRequestPull_Success() public {
        uint256 amount = TEST_AMOUNT;
        bytes32 batchId = TEST_BATCH_ID;

        vm.prank(address(minter));
        vm.expectRevert();
        assetRouter.kAssetRequestPull(tokens.usdc, amount, batchId);
    }

    function test_KAssetRequestPull_RevertZeroAmount() public {
        vm.prank(address(minter));
        vm.expectRevert(bytes(KASSETROUTER_ZERO_AMOUNT));
        assetRouter.kAssetRequestPull(tokens.usdc, 0, TEST_BATCH_ID);
    }

    function test_KAssetRequestPull_OnlyKMinter() public {
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_ONLY_KMINTER));
        assetRouter.kAssetRequestPull(tokens.usdc, TEST_AMOUNT, TEST_BATCH_ID);
    }

    /* //////////////////////////////////////////////////////////////
                    STAKING VAULT INTEGRATION TESTS
    //////////////////////////////////////////////////////////////*/

    function test_KAssetTransfer_Success() public {
        uint256 amount = TEST_AMOUNT;
        bytes32 batchId = TEST_BATCH_ID;

        vm.prank(address(alphaVault));
        vm.expectRevert(bytes(KASSETROUTER_INSUFFICIENT_VIRTUAL_BALANCE));
        assetRouter.kAssetTransfer(address(alphaVault), address(betaVault), tokens.usdc, amount, batchId);
    }

    function test_KAssetTransfer_RevertInsufficientBalance() public {
        // No virtual balance setup - should revert
        vm.prank(address(alphaVault));
        vm.expectRevert(bytes(KASSETROUTER_INSUFFICIENT_VIRTUAL_BALANCE));
        assetRouter.kAssetTransfer(address(alphaVault), address(betaVault), tokens.usdc, TEST_AMOUNT, TEST_BATCH_ID);
    }

    function test_KAssetTransfer_RevertZeroAmount() public {
        vm.prank(address(alphaVault));
        vm.expectRevert(bytes(KASSETROUTER_ZERO_AMOUNT));
        assetRouter.kAssetTransfer(address(alphaVault), address(betaVault), tokens.usdc, 0, TEST_BATCH_ID);
    }

    function test_KAssetTransfer_OnlyStakingVault() public {
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_ONLY_KSTAKING_VAULT));
        assetRouter.kAssetTransfer(address(alphaVault), address(betaVault), tokens.usdc, TEST_AMOUNT, TEST_BATCH_ID);
    }

    function test_KSharesRequestPush_Success() public {
        uint256 amount = TEST_AMOUNT;
        bytes32 batchId = TEST_BATCH_ID;

        // Only staking vault can call this function
        vm.prank(address(alphaVault));
        vm.expectEmit(true, true, false, true);
        emit IkAssetRouter.SharesRequestedPushed(address(alphaVault), batchId, amount);

        assetRouter.kSharesRequestPush(address(alphaVault), amount, batchId);

        // Verify shares are tracked correctly
        assertEq(assetRouter.getRequestedShares(address(alphaVault), batchId), amount, "Requested shares incorrect");
    }

    function test_KSharesRequestPush_RevertZeroAmount() public {
        vm.prank(address(alphaVault));
        vm.expectRevert(bytes(KASSETROUTER_ZERO_AMOUNT));
        assetRouter.kSharesRequestPush(address(alphaVault), 0, TEST_BATCH_ID);
    }

    function test_KSharesRequestPush_OnlyStakingVault() public {
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_ONLY_KSTAKING_VAULT));
        assetRouter.kSharesRequestPush(address(alphaVault), TEST_AMOUNT, TEST_BATCH_ID);
    }

    function test_KSharesRequestPull_Success() public {
        uint256 amount = TEST_AMOUNT;
        bytes32 batchId = TEST_BATCH_ID;

        // First push some shares
        vm.prank(address(alphaVault));
        assetRouter.kSharesRequestPush(address(alphaVault), amount, batchId);

        // Then pull them back
        vm.prank(address(alphaVault));
        vm.expectEmit(true, true, false, true);
        emit IkAssetRouter.SharesRequestedPulled(address(alphaVault), batchId, amount);

        assetRouter.kSharesRequestPull(address(alphaVault), amount, batchId);

        // Verify shares are back to zero
        assertEq(assetRouter.getRequestedShares(address(alphaVault), batchId), 0, "Requested shares should be zero");
    }

    function test_KSharesRequestPull_RevertZeroAmount() public {
        vm.prank(address(alphaVault));
        vm.expectRevert(bytes(KASSETROUTER_ZERO_AMOUNT));
        assetRouter.kSharesRequestPull(address(alphaVault), 0, TEST_BATCH_ID);
    }

    function test_KSharesRequestPull_OnlyStakingVault() public {
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_ONLY_KSTAKING_VAULT));
        assetRouter.kSharesRequestPull(address(alphaVault), TEST_AMOUNT, TEST_BATCH_ID);
    }

    function test_KSharesRequestPull_RevertInsufficientBalance() public {
        // Try to pull without pushing first
        vm.prank(address(alphaVault));
        vm.expectRevert();
        assetRouter.kSharesRequestPull(address(alphaVault), TEST_AMOUNT, TEST_BATCH_ID);
    }

    /* //////////////////////////////////////////////////////////////
                    TIMELOCK SETTLEMENT TESTS
    //////////////////////////////////////////////////////////////*/

    function test_ProposeSettleBatch_Success() public {
        bytes32 batchId = TEST_BATCH_ID;

        vm.prank(users.relayer);
        dnVault.closeBatch(batchId, true);

        // Propose settlement
        vm.prank(users.relayer);
        vm.expectEmit(false, true, true, false);
        emit IkAssetRouter.SettlementProposed(
            bytes32(0), // We don't know the exact proposalId yet
            address(dnVault),
            batchId,
            TEST_TOTAL_ASSETS,
            0,
            0,
            block.timestamp + 1, // executeAfter with 1 second cooldown,
            0,
            0
        );

        testProposalId = assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), batchId, TEST_TOTAL_ASSETS, 0, 0);

        // Verify proposal was stored correctly
        IkAssetRouter.VaultSettlementProposal memory proposal = assetRouter.getSettlementProposal(testProposalId);
        assertEq(proposal.asset, tokens.usdc, "Asset incorrect");
        assertEq(proposal.vault, address(dnVault), "Vault incorrect");
        assertEq(proposal.batchId, batchId, "BatchId incorrect");
        assertEq(proposal.totalAssets, TEST_TOTAL_ASSETS, "Total assets incorrect");
        assertEq(proposal.netted, 0, "Netted amount incorrect");
        assertEq(uint256(proposal.yield), TEST_TOTAL_ASSETS, "Yield incorrect");
        assertEq(proposal.executeAfter, block.timestamp + 1, "ExecuteAfter incorrect");
    }

    function test_ProposeSettleBatch_OnlyRelayer() public {
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_WRONG_ROLE));
        assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), TEST_BATCH_ID, TEST_TOTAL_ASSETS, 0, 0);
    }

    function test_ProposeSettleBatch_RevertWhenPaused() public {
        // Pause contract
        vm.prank(users.emergencyAdmin);
        assetRouter.setPaused(true);

        vm.prank(users.relayer);
        vm.expectRevert(bytes(KASSETROUTER_IS_PAUSED));
        assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), TEST_BATCH_ID, TEST_TOTAL_ASSETS, 0, 0);
    }

    function test_ExecuteSettleBatch_AfterCooldown() public {
        vm.prank(users.relayer);
        dnVault.closeBatch(TEST_BATCH_ID, true);

        // Create a proposal
        vm.prank(users.relayer);
        testProposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), TEST_BATCH_ID, TEST_TOTAL_ASSETS, 0, 0);

        // Wait for cooldown (1 second in our setup)
        vm.warp(block.timestamp + 2);

        // Anyone should be able to execute after cooldown
        vm.prank(users.alice);
        assetRouter.executeSettleBatch(testProposalId);
    }

    function test_ExecuteSettleBatch_RevertBeforeCooldown() public {
        vm.prank(users.relayer);
        dnVault.closeBatch(TEST_BATCH_ID, true);

        // Create a proposal
        vm.prank(users.relayer);
        testProposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), TEST_BATCH_ID, TEST_TOTAL_ASSETS, 0, 0);

        // Try to execute immediately (should fail due to cooldown)
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_COOLDOOWN_IS_UP));
        assetRouter.executeSettleBatch(testProposalId);
    }

    function test_ExecuteSettleBatch_RevertProposalNotFound() public {
        bytes32 fakeProposalId = keccak256("fake");

        vm.warp(block.timestamp + 2);
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_PROPOSAL_NOT_FOUND));
        assetRouter.executeSettleBatch(fakeProposalId);
    }

    function test_ExecuteSettleBatch_RevertWhenPaused() public {
        vm.prank(users.relayer);
        dnVault.closeBatch(TEST_BATCH_ID, true);

        // Create a proposal
        vm.prank(users.relayer);
        testProposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), TEST_BATCH_ID, TEST_TOTAL_ASSETS, 0, 0);

        // Wait for cooldown
        vm.warp(block.timestamp + 2);

        // Pause contract
        vm.prank(users.emergencyAdmin);
        assetRouter.setPaused(true);

        // Try to execute
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_IS_PAUSED));
        assetRouter.executeSettleBatch(testProposalId);
    }

    function test_CanExecuteProposal() public {
        // Test non-existent proposal
        bytes32 fakeProposalId = keccak256("fake");
        (bool canExecute, string memory reason) = assetRouter.canExecuteProposal(fakeProposalId);
        assertFalse(canExecute, "Should not be able to execute non-existent proposal");
        assertEq(reason, "Proposal not found", "Reason incorrect");

        vm.prank(users.relayer);
        dnVault.closeBatch(TEST_BATCH_ID, true);

        vm.prank(users.relayer);
        testProposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), TEST_BATCH_ID, TEST_TOTAL_ASSETS, 0, 0);

        // Test before cooldown
        (canExecute, reason) = assetRouter.canExecuteProposal(testProposalId);
        assertFalse(canExecute, "Should not be able to execute before cooldown");
        assertEq(reason, "Cooldown not passed", "Reason incorrect");

        // Wait for cooldown
        vm.warp(block.timestamp + 2);

        // Test after cooldown
        (canExecute, reason) = assetRouter.canExecuteProposal(testProposalId);
        assertTrue(canExecute, "Should be able to execute after cooldown");
        assertEq(reason, "", "Reason should be empty");
    }

    /* //////////////////////////////////////////////////////////////
                    COOLDOWN MANAGEMENT TESTS
    //////////////////////////////////////////////////////////////*/

    function test_SetSettlementCooldown_Success() public {
        uint256 newCooldown = 2 hours;

        vm.prank(users.admin);
        vm.expectEmit(true, true, false, true);
        emit IkAssetRouter.SettlementCooldownUpdated(1, newCooldown);

        assetRouter.setSettlementCooldown(newCooldown);

        assertEq(assetRouter.getSettlementCooldown(), newCooldown, "Cooldown not updated");
    }

    function test_SetSettlementCooldown_OnlyAdmin() public {
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_WRONG_ROLE));
        assetRouter.setSettlementCooldown(2 hours);
    }

    /* //////////////////////////////////////////////////////////////
                        ADMIN FUNCTION TESTS
    //////////////////////////////////////////////////////////////*/

    function test_SetPaused_Success() public {
        assertFalse(assetRouter.isPaused(), "Should be unpaused initially");

        vm.prank(users.emergencyAdmin);
        vm.expectEmit(false, false, false, true);
        emit kBase.Paused(true);

        assetRouter.setPaused(true);

        assertTrue(assetRouter.isPaused(), "Should be paused");

        // Test unpause
        vm.prank(users.emergencyAdmin);
        assetRouter.setPaused(false);

        assertFalse(assetRouter.isPaused(), "Should be unpaused");
    }

    function test_SetPaused_OnlyEmergencyAdmin() public {
        vm.prank(users.alice);
        vm.expectRevert(bytes(KBASE_WRONG_ROLE));
        assetRouter.setPaused(true);
    }

    /* //////////////////////////////////////////////////////////////
                        VIEW FUNCTION TESTS
    //////////////////////////////////////////////////////////////*/

    function test_GetBatchIdBalances() public view {
        bytes32 batchId = TEST_BATCH_ID;

        // Initially zero for any vault/batch combination
        (uint256 dep, uint256 req) = assetRouter.getBatchIdBalances(address(alphaVault), batchId);
        assertEq(dep, 0, "Deposited should be zero initially");
        assertEq(req, 0, "Requested should be zero initially");

        // Test with different vault
        (dep, req) = assetRouter.getBatchIdBalances(address(dnVault), batchId);
        assertEq(dep, 0, "DN vault deposited should be zero initially");
        assertEq(req, 0, "DN vault requested should be zero initially");
    }

    function test_GetBatchIdBalances_WithData() public {
        bytes32 batchId = TEST_BATCH_ID;
        uint256 depositAmount = TEST_AMOUNT;

        // Deposit via kMinter
        mockUSDC.mint(address(minter), depositAmount);
        vm.prank(address(minter));
        // forge-lint: disable-next-line(erc20-unchecked-transfer)
        IERC20(tokens.usdc).transfer(address(assetRouter), depositAmount);
        vm.prank(address(minter));
        assetRouter.kAssetPush(tokens.usdc, depositAmount, batchId);

        // Verify deposit balance
        (uint256 dep, uint256 req) = assetRouter.getBatchIdBalances(address(minter), batchId);
        assertEq(dep, depositAmount, "Deposited amount incorrect");
        assertEq(req, 0, "Requested should be zero without request");
    }

    function test_GetRequestedShares() public {
        uint256 amount = TEST_AMOUNT;
        bytes32 batchId = TEST_BATCH_ID;

        // Initially zero
        assertEq(assetRouter.getRequestedShares(address(alphaVault), batchId), 0, "Should be zero initially");

        // Push shares first
        vm.prank(address(alphaVault));
        assetRouter.kSharesRequestPush(address(alphaVault), amount, batchId);

        assertEq(
            assetRouter.getRequestedShares(address(alphaVault), batchId),
            amount,
            "Should return correct requested shares after push"
        );

        // Pull shares back
        vm.prank(address(alphaVault));
        assetRouter.kSharesRequestPull(address(alphaVault), amount, batchId);

        assertEq(assetRouter.getRequestedShares(address(alphaVault), batchId), 0, "Should be zero after pull");
    }

    function test_GetRequestedShares_MultipleBatches() public {
        uint256 amount1 = TEST_AMOUNT;
        uint256 amount2 = TEST_AMOUNT * 2;
        bytes32 batchId1 = TEST_BATCH_ID;
        bytes32 batchId2 = bytes32(uint256(TEST_BATCH_ID) + 1);

        // Push shares for different batches
        vm.prank(address(alphaVault));
        assetRouter.kSharesRequestPush(address(alphaVault), amount1, batchId1);

        vm.prank(address(alphaVault));
        assetRouter.kSharesRequestPush(address(alphaVault), amount2, batchId2);

        // Verify each batch tracks separately
        assertEq(assetRouter.getRequestedShares(address(alphaVault), batchId1), amount1, "Batch 1 shares incorrect");
        assertEq(assetRouter.getRequestedShares(address(alphaVault), batchId2), amount2, "Batch 2 shares incorrect");
    }

    function test_GetSettlementProposal() public {
        // Test non-existent proposal
        bytes32 fakeProposalId = keccak256("fake");
        IkAssetRouter.VaultSettlementProposal memory proposal = assetRouter.getSettlementProposal(fakeProposalId);
        assertEq(proposal.executeAfter, 0, "Non-existent proposal should have zero executeAfter");

        vm.prank(users.relayer);
        dnVault.closeBatch(TEST_BATCH_ID, true);

        // Create a proposal
        vm.prank(users.relayer);
        testProposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), TEST_BATCH_ID, TEST_TOTAL_ASSETS, 0, 0);

        // Get and verify proposal
        proposal = assetRouter.getSettlementProposal(testProposalId);
        assertEq(proposal.asset, tokens.usdc, "Asset incorrect");
        assertEq(proposal.vault, address(dnVault), "Vault incorrect");
        assertEq(proposal.batchId, TEST_BATCH_ID, "BatchId incorrect");
        assertEq(proposal.totalAssets, TEST_TOTAL_ASSETS, "Total assets incorrect");
        assertEq(proposal.netted, 0, "Netted incorrect");
        assertEq(uint256(proposal.yield), TEST_TOTAL_ASSETS, "Yield incorrect");
        assertGt(proposal.executeAfter, 0, "executeAfter should be set");
    }

    function test_GetSettlementCooldown() public {
        // Initially set to 1 second in setUp
        assertEq(assetRouter.getSettlementCooldown(), 1, "Initial cooldown incorrect");

        // Change cooldown
        uint256 newCooldown = 5 hours;
        vm.prank(users.admin);
        assetRouter.setSettlementCooldown(newCooldown);

        assertEq(assetRouter.getSettlementCooldown(), newCooldown, "Updated cooldown incorrect");
    }

    function test_GetRegistry() public view {
        assertEq(address(assetRouter.registry()), address(registry), "Registry address incorrect");
    }

    function test_IsPaused() public {
        assertFalse(assetRouter.isPaused(), "Should be unpaused initially");

        vm.prank(users.emergencyAdmin);
        assetRouter.setPaused(true);

        assertTrue(assetRouter.isPaused(), "Should be paused after setPaused");
    }

    /* //////////////////////////////////////////////////////////////
                        CONTRACT INFO TESTS
    //////////////////////////////////////////////////////////////*/

    function test_ContractInfo() public view {
        assertEq(assetRouter.contractName(), "kAssetRouter", "Contract name incorrect");
        assertEq(assetRouter.contractVersion(), "1.0.0", "Contract version incorrect");
    }

    function test_ReceiveETH() public {
        uint256 amount = 1 ether;

        // Send ETH to contract
        vm.deal(users.alice, amount);
        vm.prank(users.alice);
        (bool success,) = address(assetRouter).call{ value: amount }("");

        assertTrue(success, "ETH transfer should succeed");
        assertEq(address(assetRouter).balance, amount, "Contract should receive ETH");
    }

    /* //////////////////////////////////////////////////////////////
                        UPGRADE FUNCTION TESTS
    //////////////////////////////////////////////////////////////*/

    function test_AuthorizeUpgrade_OnlyAdmin() public {
        address newImpl = address(new kAssetRouter());

        // Non-admin should fail
        vm.prank(users.alice);
        vm.expectRevert(bytes(KASSETROUTER_WRONG_ROLE));
        assetRouter.upgradeToAndCall(newImpl, "");

        // Test authorization check passes for admin
        assertTrue(true, "Authorization test completed");
    }

    function test_AuthorizeUpgrade_RevertZeroAddress() public {
        // Should revert when trying to upgrade to zero address
        vm.prank(users.admin);
        vm.expectRevert(bytes(KASSETROUTER_ZERO_ADDRESS));
        assetRouter.upgradeToAndCall(address(0), "");
    }

    /* //////////////////////////////////////////////////////////////
                        EDGE CASES AND SECURITY TESTS
    //////////////////////////////////////////////////////////////*/

    function test_MaximumAmounts() public {
        uint256 maxAmount = type(uint128).max; // Large amount within limits
        bytes32 batchId = TEST_BATCH_ID;

        // Test with maximum amount in asset push
        mockUSDC.mint(address(minter), maxAmount);
        vm.prank(address(minter));
        // forge-lint: disable-next-line(erc20-unchecked-transfer)
        IERC20(tokens.usdc).transfer(address(assetRouter), maxAmount);
        vm.prank(address(minter));
        assetRouter.kAssetPush(tokens.usdc, maxAmount, batchId);

        (uint256 deposited,) = assetRouter.getBatchIdBalances(address(minter), batchId);
        assertEq(deposited, maxAmount, "Max amount deposit failed");
    }

    function test_MultiAssetSupport() public {
        bytes32 batchId1 = TEST_BATCH_ID;
        bytes32 batchId2 = bytes32(uint256(TEST_BATCH_ID) + 1);
        uint256 amount = TEST_AMOUNT;

        // Test USDC
        mockUSDC.mint(address(minter), amount);
        vm.prank(address(minter));
        // forge-lint: disable-next-line(erc20-unchecked-transfer)
        IERC20(tokens.usdc).transfer(address(assetRouter), amount);
        vm.prank(address(minter));
        assetRouter.kAssetPush(tokens.usdc, amount, batchId1);

        // Test WBTC
        mockWBTC.mint(address(minter), amount);
        vm.prank(address(minter));
        // forge-lint: disable-next-line(erc20-unchecked-transfer)
        IERC20(tokens.wbtc).transfer(address(assetRouter), amount);
        vm.prank(address(minter));
        assetRouter.kAssetPush(tokens.wbtc, amount, batchId2);

        // Verify both assets are handled separately
        (uint256 usdcDep,) = assetRouter.getBatchIdBalances(address(minter), batchId1);
        (uint256 wbtcDep,) = assetRouter.getBatchIdBalances(address(minter), batchId2);

        assertEq(usdcDep, amount, "tokens.usdc deposit failed");
        assertEq(wbtcDep, amount, "WBTC deposit failed");
    }

    function test_BatchIdCollisionResistance() public {
        bytes32 batchId1 = keccak256(abi.encode("batch1"));
        bytes32 batchId2 = keccak256(abi.encode("batch2"));
        uint256 amount = TEST_AMOUNT;

        // Same vault, different batch IDs should be independent
        vm.prank(address(alphaVault));
        assetRouter.kSharesRequestPush(address(alphaVault), amount, batchId1);

        vm.prank(address(alphaVault));
        assetRouter.kSharesRequestPush(address(alphaVault), amount * 2, batchId2);

        assertEq(assetRouter.getRequestedShares(address(alphaVault), batchId1), amount, "Batch 1 collision");
        assertEq(assetRouter.getRequestedShares(address(alphaVault), batchId2), amount * 2, "Batch 2 collision");
    }

    function test_ReentrancyProtection() public {
        vm.prank(users.relayer);
        dnVault.closeBatch(TEST_BATCH_ID, true);

        // Create a proposal first
        vm.prank(users.relayer);
        bytes32 proposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), TEST_BATCH_ID, TEST_TOTAL_ASSETS, 0, 0);

        // Wait for cooldown
        vm.warp(block.timestamp + 2);

        vm.prank(users.alice);
        assetRouter.executeSettleBatch(proposalId);
    }

    function test_ExtremeCooldownValues() public {
        // Test setting maximum allowed cooldown (1 day)
        uint256 maxCooldown = 1 days;
        vm.prank(users.admin);
        assetRouter.setSettlementCooldown(maxCooldown);
        assertEq(assetRouter.getSettlementCooldown(), maxCooldown, "Max cooldown failed");

        // Test setting back to small value
        vm.prank(users.admin);
        assetRouter.setSettlementCooldown(1);
        assertEq(assetRouter.getSettlementCooldown(), 1, "Small cooldown failed");

        // Test that exceeding max cooldown reverts
        vm.prank(users.admin);
        vm.expectRevert(bytes(KASSETROUTER_INVALID_COOLDOWN));
        assetRouter.setSettlementCooldown(2 days);
    }

    function test_PausedStateCoverage() public {
        // Pause contract
        vm.prank(users.emergencyAdmin);
        assetRouter.setPaused(true);

        // All critical functions should revert when paused
        vm.prank(address(minter));
        vm.expectRevert(bytes(KASSETROUTER_IS_PAUSED));
        assetRouter.kAssetPush(tokens.usdc, TEST_AMOUNT, TEST_BATCH_ID);

        vm.prank(address(minter));
        vm.expectRevert(bytes(KASSETROUTER_IS_PAUSED));
        assetRouter.kAssetRequestPull(tokens.usdc, TEST_AMOUNT, TEST_BATCH_ID);

        vm.prank(address(alphaVault));
        vm.expectRevert(bytes(KASSETROUTER_IS_PAUSED));
        assetRouter.kSharesRequestPush(address(alphaVault), TEST_AMOUNT, TEST_BATCH_ID);

        vm.prank(users.relayer);
        vm.expectRevert(bytes(KASSETROUTER_IS_PAUSED));
        assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), TEST_BATCH_ID, TEST_TOTAL_ASSETS, 0, 0);

        // View functions should still work
        assertEq(assetRouter.getSettlementCooldown(), 1, "View functions should work when paused");
    }

    function test_EventEmissions() public {
        uint256 amount = TEST_AMOUNT;
        bytes32 batchId = TEST_BATCH_ID;

        // Test all events are properly emitted
        mockUSDC.mint(address(minter), amount);
        vm.prank(address(minter));
        // forge-lint: disable-next-line(erc20-unchecked-transfer)
        IERC20(tokens.usdc).transfer(address(assetRouter), amount);

        vm.prank(address(minter));
        vm.expectEmit(true, false, false, true);
        emit IkAssetRouter.AssetsPushed(address(minter), amount);
        assetRouter.kAssetPush(tokens.usdc, amount, batchId);

        // Test share events
        vm.prank(address(alphaVault));
        vm.expectEmit(true, true, false, true);
        emit IkAssetRouter.SharesRequestedPushed(address(alphaVault), batchId, amount);
        assetRouter.kSharesRequestPush(address(alphaVault), amount, batchId);

        vm.prank(users.relayer);
        dnVault.closeBatch(batchId, true);

        // Test settlement proposal event
        vm.prank(users.relayer);
        vm.expectEmit(false, true, true, false); // Don't check first indexed (proposalId)
        emit IkAssetRouter.SettlementProposed(
            bytes32(0), // proposalId - we don't know it yet
            address(dnVault),
            batchId,
            TEST_TOTAL_ASSETS,
            TEST_NETTED,
            // casting to 'int256' is safe because TEST_PROFIT fits in int256
            // forge-lint: disable-next-line(unsafe-typecast)
            int256(TEST_PROFIT),
            block.timestamp + 1,
            0,
            0
        );
        assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), batchId, TEST_TOTAL_ASSETS, 0, 0);
    }

    function test_GasOptimization() public {
        // Test multiple operations in single batch
        bytes32 batchId = TEST_BATCH_ID;
        uint256 amount = TEST_AMOUNT;

        // Multiple shares requests should be gas efficient
        uint256 gasStart = gasleft();

        vm.prank(address(alphaVault));
        assetRouter.kSharesRequestPush(address(alphaVault), amount, batchId);

        vm.prank(address(alphaVault));
        assetRouter.kSharesRequestPush(address(alphaVault), amount, batchId);

        uint256 gasUsed = gasStart - gasleft();

        // Verify operations complete successfully
        assertTrue(gasUsed > 0, "Operations should consume gas");
    }

    /* //////////////////////////////////////////////////////////////
                    INTEGRATION SCENARIO TESTS
    //////////////////////////////////////////////////////////////*/

    function test_SettlementFlow_Complete() public {
        bytes32 batchId = TEST_BATCH_ID;

        vm.prank(users.relayer);
        dnVault.closeBatch(batchId, true);

        // Step 1: Propose settlement
        vm.prank(users.relayer);
        bytes32 proposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), batchId, TEST_TOTAL_ASSETS, 0, 0);

        // Step 2: Check cannot execute before cooldown
        (bool canExecute, string memory reason) = assetRouter.canExecuteProposal(proposalId);
        assertFalse(canExecute, "Should not be able to execute immediately");
        assertEq(reason, "Cooldown not passed", "Should indicate cooldown not passed");

        // Step 3: Wait for cooldown
        vm.warp(block.timestamp + 2); // Wait 2 seconds (cooldown is 1 second)

        // Step 4: Verify can execute now
        (canExecute, reason) = assetRouter.canExecuteProposal(proposalId);
        assertTrue(canExecute, "Should be able to execute after cooldown");
        assertEq(reason, "", "No reason should be given when executable");
    }

    function test_CancelProposal_Success() public {
        bytes32 batchId = TEST_BATCH_ID;

        vm.prank(users.relayer);
        dnVault.closeBatch(batchId, true);

        // Create proposal
        vm.prank(users.relayer);
        bytes32 proposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), batchId, TEST_TOTAL_ASSETS, 0, 0);

        // Cancel proposal
        vm.prank(users.guardian);
        vm.expectEmit(true, true, true, false);
        emit IkAssetRouter.SettlementCancelled(proposalId, address(dnVault), batchId);
        assetRouter.cancelProposal(proposalId);

        // Cannot execute cancelled proposal
        vm.prank(users.relayer);
        vm.warp(block.timestamp + 2);
        vm.expectRevert(bytes(KASSETROUTER_PROPOSAL_NOT_FOUND));
        assetRouter.executeSettleBatch(proposalId);
    }

    function test_CancelProposal_RevertAlreadyCancelled() public {
        bytes32 batchId = TEST_BATCH_ID;

        vm.prank(users.relayer);
        dnVault.closeBatch(batchId, true);

        // Create and cancel proposal
        vm.prank(users.relayer);
        bytes32 proposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), batchId, TEST_TOTAL_ASSETS, 0, 0);

        vm.prank(users.guardian);
        assetRouter.cancelProposal(proposalId);

        // Try to cancel again
        vm.prank(users.guardian);
        vm.expectRevert(bytes(KASSETROUTER_PROPOSAL_NOT_FOUND));
        assetRouter.cancelProposal(proposalId);
    }

    function test_MultipleProposals_SameBatch_Revert() public {
        bytes32 batchId = TEST_BATCH_ID;

        vm.prank(users.relayer);
        dnVault.closeBatch(batchId, true);

        // Create first proposal
        vm.prank(users.relayer);
        bytes32 proposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), batchId, TEST_TOTAL_ASSETS, 0, 0);

        // Create second proposal for same batch (different timestamp makes different ID)
        vm.warp(block.timestamp + 1);
        vm.prank(users.relayer);
        vm.expectRevert(bytes(KASSETROUTER_BATCH_ID_PROPOSED));
        proposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), batchId, TEST_TOTAL_ASSETS + 1000, 0, 0);
    }

    function test_CooldownEdgeCases() public {
        vm.prank(users.admin);
        assetRouter.setSettlementCooldown(2);

        bytes32 batchId = TEST_BATCH_ID;

        vm.prank(users.relayer);
        dnVault.closeBatch(batchId, true);

        // Create proposal
        vm.prank(users.relayer);
        bytes32 proposalId =
            assetRouter.proposeSettleBatch(tokens.usdc, address(dnVault), batchId, TEST_TOTAL_ASSETS, 0, 0);

        // Test exactly at cooldown boundary (1 second)
        vm.warp(block.timestamp + 1);

        // Should still not be executable (need to pass cooldown, not just reach it)
        vm.expectRevert(bytes(KASSETROUTER_COOLDOOWN_IS_UP));
        assetRouter.executeSettleBatch(proposalId);

        // One more second should make it executable
        vm.warp(block.timestamp + 3);
        (bool canExecute,) = assetRouter.canExecuteProposal(proposalId);
        assertTrue(canExecute, "Should be executable after cooldown");
    }
}
